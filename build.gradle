buildscript {
    apply from: "${rootProject.projectDir}/buildSrc/repositories.gradle"
    repositories extRepositories
    apply from: "${rootProject.projectDir}/buildSrc/buildscript-dependencies.gradle"
    dependencies extDependencies
}

group 'com.hillel.evo'
version '1.0-SNAPSHOT'

configure(allprojects) { project ->

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'application'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply from: "${rootProject.projectDir}/buildSrc/dependencies.gradle"
    apply from: "${rootProject.projectDir}/buildSrc/analyze.gradle"
    
    sourceCompatibility = 11

    repositories extRepositories

    test {
        useJUnitPlatform {
            excludeTags 'slow'
            includeEngines 'junit-jupiter'
        }
        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
        failFast = false
    }

    application {
        mainClassName = 'com.hillel.evo.adviser.starter.AdviserStarter'
    }

    bootJar {
        manifest {
            attributes 'Start-Class': 'com.hillel.evo.adviser.starter.AdviserStarter'
        }
    }

    bootRun {
        systemProperties = System.properties
    }
    
    dependencies {
        implementation boot.starterWeb
        implementation boot.starterJpa
        runtimeOnly db.h2
        runtimeOnly  db.postgres
        runtimeOnly db.awsSecretsmanager

        testImplementation boot.starterTest
        testImplementation testing.junit5
        testImplementation testing.junit5Platform
        testImplementation testing.junit5Mockhito

        implementation misc.swagger
        implementation misc.swaggerUI
        
        annotationProcessor utils.lombok

    }
}

wrapper {
    version = '5.6.2'
}

project(':search') {
    dependencies {
        implementation hibernate.search
        implementation hibernate.em
        implementation hibernate.spatial
    }
}

project(":auto-aid-adviser-main") {

    dependencies {
        implementation project(':search')
    }
}


