buildscript {
    apply from: "${rootProject.projectDir}/buildSrc/repositories.gradle"
    repositories extRepositories
    apply from: "${rootProject.projectDir}/buildSrc/buildscript-dependencies.gradle"
    dependencies extDependencies
}

group 'com.hillel.evo'
version '1.0-SNAPSHOT'

configure(allprojects) { project ->
    apply plugin: 'application'
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply from: "${rootProject.projectDir}/buildSrc/dependencies.gradle"
    apply from: "${rootProject.projectDir}/buildSrc/analyze.gradle"

    sourceCompatibility = 11

    repositories extRepositories

    test {
        useJUnitPlatform {
            excludeTags 'slow'
            includeEngines 'junit-jupiter'
        }
        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
        failFast = false
    }

    mainClassName = 'com.hillel.evo.adviser.AdviserStarter'

    springBoot {
        mainClassName = 'com.hillel.evo.adviser.AdviserStarter'
    }

    bootRun {
        systemProperties = System.properties
    }
    
    dependencies {
        implementation boot.starterWeb
        implementation boot.starterJpa
        implementation boot.starterMail
        implementation boot.starterThyme        
        implementation utils.jaxbApi
        implementation utils.jaxbImpl
        implementation utils.jaxbCore
        testImplementation boot.starterTest

        runtimeOnly db.h2
        runtimeOnly db.postgres
        runtimeOnly db.awsSecretsmanager
        implementation misc.awsSdk
        implementation misc.swagger
        implementation misc.swaggerUI

        annotationProcessor utils.lombok
        annotationProcessor boot.annotationProcessor
        compileOnly utils.lombok

        implementation db.postgres
        testImplementation db.h2
    }
}

wrapper {
    version = '5.6.2'
}


project(":user-service") {
    bootJar {
        enabled = false
    }
    jar {
        enabled = true
    }
}

project(":auto-aid-adviser-email") {
    bootJar {
        enabled = false
    }
    jar {
        enabled = true
    }
}

project(":auto-aid-adviser-main") {
    jar {
        enabled = true
    }
    bootJar {
        classifier = 'boot'
    }
    dependencies {
        implementation project(':user-service')
        implementation project(':auto-aid-adviser-email')
    }
}
